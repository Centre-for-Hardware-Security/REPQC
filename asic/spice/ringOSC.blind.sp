$ Spice netlist generated by v2lvs

Venable enablepin 0 PWL (0ns 0.0V 0.99ns 0V 1ns 0.8V)

.temp 25.0
.param HSIMVDD = 0.8
.param HSIMALLOWEDDV = 0.08
.param HSIMSPEED = 5

Vvdd1 VDD1 g0 0.8
Vvdd2 VDD2 g1 0.8
Vvdd3 VDD3 g2 0.8
Vvdd4 VDD4 g3 0.8
Vvss1 VSS1 g0 0
Vvss2 VSS2 g1 0
Vvss3 VSS3 g2 0
Vvss4 VSS4 g3 0
Rfake1 g0 0 100M
Rfake2 g1 0 100M
Rfake3 g2 0 100M
Rfake4 g3 0 100M

* these files are NDA protected and cannot be shared. the first one contains LPE views of std cells
* the second contains the transistor spice decks with MC support
.include cells.sp
.include libs.sp

.SUBCKT RING_OSC enable sel[1] sel[0] osc_out VDD VSS
.CONNECT OUT_MUX_DELAYED SUPERSLOWED
.CONNECT osc_out OUT_MUX_DELAYED

XNAND enable OUT_MUX_DELAYED OUT_NAND VDD VSS ND2D0BWP30P140HVT

XNOT_1 OUT_NAND OUT_NOT_1 VDD VSS INVD0BWP30P140HVT

XNOT_2 OUT_NOT_1 OUT_NOT_2 VDD VSS INVD0BWP30P140HVT

XNOT_3 OUT_NOT_2 OUT_NOT_3 VDD VSS INVD0BWP30P140HVT

XNOT_4 OUT_NOT_3 OUT_NOT_4 VDD VSS INVD2BWP30P140HVT

XDEL_1 OUT_NOT_4 OUT_DEL_A VDD VSS DEL025D1BWP30P140HVT
XLOAD_1 OUT_DEL_A NOWHERE1 VDD VSS INVD1P5BWP30P140HVT

XDEL_2 OUT_NOT_4 OUT_DEL_B VDD VSS DEL050MD1BWP30P140HVT
XLOAD_2 OUT_DEL_B NOWHERE2 VDD VSS INVD1P5BWP30P140HVT

XDEL_3 OUT_NOT_4 OUT_DEL_C VDD VSS DEL075MD1BWP30P140HVT
XLOAD_3 OUT_DEL_C NOWHERE3 VDD VSS INVD1P5BWP30P140HVT

XDEL_4 OUT_NOT_4 OUT_DEL_D VDD VSS DEL100MD1BWP30P140HVT
XLOAD_4 OUT_DEL_D NOWHERE4 VDD VSS INVD1P5BWP30P140HVT

$ the order of the ports, from fast to slow, is B D A C
XMUX_4_to_1 OUT_DEL_A OUT_DEL_B OUT_DEL_C OUT_DEL_D sel[0] sel[1] OUT_MUX VDD VSS MUX4D0BWP30P140HVT

XDEL_SMALL1 OUT_MUX SLOW VDD VSS DEL050MD1BWP30P140HVT 
XDEL_BIG1 SLOW SLOWED VDD VSS DEL100MD1BWP30P140HVT 
XDEL_BIG2 SLOWED SSLOWED VDD VSS DEL100MD1BWP30P140HVT 
XDEL_BIG3 SSLOWED SUPERSLOWED VDD VSS DEL100MD1BWP30P140HVT 

.ENDS

Xmyring1 enablepin VSS1 VSS1 osc1 VDD1 VSS1 RING_OSC 
Xmyring2 enablepin VSS2 VDD2 osc2 VDD2 VSS2 RING_OSC  
Xmyring3 enablepin VDD3 VSS3 osc3 VDD3 VSS3 RING_OSC  
Xmyring4 enablepin VDD4 VDD4 osc4 VDD4 VSS4 RING_OSC  

* enable the first line for single sim, enable the second line for MC
*.tran STEP=1p STOP=20n
.tran STEP=1f STOP=30n sweep monte = 1000
.print tran V(osc1) V(osc2) V(osc3) V(osc4)
.print tran V(Xmyring4.OUT_MUX)
.print tran V(Xmyring4.OUT_DEL_A)
.print tran V(Xmyring4.OUT_DEL_B)
.print tran V(Xmyring4.OUT_DEL_C)
.print tran V(Xmyring4.OUT_DEL_D)
.meas tran t1 trig V(osc1) val=0.4 cross=5 targ V(osc1) val=0.4 cross=15
.meas tran t2 trig V(osc2) val=0.4 cross=5 targ V(osc2) val=0.4 cross=15
.meas tran t3 trig V(osc3) val=0.4 cross=5 targ V(osc3) val=0.4 cross=15
.meas tran t4 trig V(osc4) val=0.4 cross=5 targ V(osc4) val=0.4 cross=15
.meas period1 param='t1/5'
.meas period2 param='t2/5'
.meas period3 param='t3/5'
.meas period4 param='t4/5'
.meas freq1 param='1/period1'
.meas freq2 param='1/period2'
.meas freq3 param='1/period3'
.meas freq4 param='1/period4'

.meas TRAN iavg1 AVG i(Vvdd1) FROM=10n TO=20n
.meas TRAN iavg2 AVG i(Vvdd2) FROM=10n TO=20n
.meas TRAN iavg3 AVG i(Vvdd3) FROM=10n TO=20n
.meas TRAN iavg4 AVG i(Vvdd4) FROM=10n TO=20n
.meas TRAN power1 PARAM='-iavg1*0.8'
.meas TRAN power2 PARAM='-iavg2*0.8'
.meas TRAN power3 PARAM='-iavg3*0.8'
.meas TRAN power4 PARAM='-iavg4*0.8'



